// TODO: - decorators
//       - continuation line
//       - augassign 
//       - dotted names 
//       - boolean expr in factor
//       - class 
//       - io files  
//       - command line args 
//       - OpFactor: add '//'
//       - OpFactorSigned: add '~'
//       - Atom: add dictorsetmaker 
//       - Atom: add  '...'
//       - Atom: add yield expr 
//       - AtomExpr: allow multiple trailers 
//       - DecoratedStmt: add async_funcdef 
//       - FunctionHeaderStmt: add HeaderResults  
//       - MethodHeaderStmt: add HeaderResults  
//       -  
//       -  
//       -  
//       -  

import openmp.grammar
import imports.grammar

Pyccel:
  (statements*=Statement)
;

Statement:
    SimpleStmt 
  | CompoundStmt
;

SimpleStmt:
  SmallStmt (';' SmallStmt)* (';')?
;

SmallStmt:
    AnnotatedStmt 
  | CommentStmt 
  | PassStmt 
  | FlowStmt 
  | DelStmt 
  | ImportStmt 
  | ConstructorStmt 
  | DeclarationStmt 
  | ExprStmt 
  | PythonStdLibStmt
;

FlowStmt:
    BreakStmt 
  | ContinueStmt 
  | ReturnStmt 
  | RaiseStmt 
  | YieldStmt
;

CompoundStmt:
    IfStmt 
  | ForStmt 
  | DecoratedStmt
  | WhileStmt
;

AnnotatedStmt:
    FunctionHeaderStmt 
  | ClassHeaderStmt
  | MethodHeaderStmt
  | OpenmpStmt
;

ExprStmt: 
    StencilStmt 
  | ThreadStmt 
  | ExecStmt 
  | EvalStmt 
  | AssignStmt 
  | MultiAssignStmt
;

SuiteStmt: 
//  SimpleStmt 
//  | 
  INDENT
  stmts+=Statement
  DEDENT
;

//DecoratorStmt: 
//  '@' dotted_name ( '(' (arglist)? ')' )? NEWLINE
//;

DecoratedStmt: 
  FunctionDefStmt | ClassDefStmt
;

FunctionDefStmt: 
  DEF name=ID '(' args*=ID[','] ')' ':'
  body=SuiteStmt
;

ClassDefStmt:
  CLASS name=ID '(' base=ID ')' ':'
  body=SuiteStmt
;

PassStmt: label=PASS;
BreakStmt: label=BREAK;
ContinueStmt: label=CONTINUE;
RaiseStmt: label=RAISE;
YieldStmt: label=YIELD;

ReturnStmt:
  label=RETURN (variables*=Expression[','])
;

DelStmt:
  DEL variables*=AtomExpr[',']
;

CommentStmt:
//  '#' text=/[^x].*$/  
  '#' text=/.*$/
;

ClassHeaderStmt: 
  '#$' 'header' 'class'  name=ID '(' options+=ClassOptions[','] ')'
;

MethodHeaderStmt: 
  '#$' 'header' 'method' name=ID '(' decs*=DeclareAtom[','] ')'
  (results=HeaderResults)?
;

FunctionHeaderStmt: 
  '#$' 'header' (kind=FunctionKind)? name=ID '(' decs*=DeclareAtom[','] ')'
  (results=HeaderResults)?
;

FunctionKind: 'function' | 'procedure';

ClassOptions: 'abstract' | 'private' | 'public';
DeclareAtom: dtype=ID (trailer=TrailerSubscriptList)?;
HeaderResults: 'results' '(' decs*=DeclareAtom[','] ')'; 

DeclarationStmt: datatype=DataType variables+=ID[','];

DataType:
    'double' 
  | 'float' 
  | 'int' 
  | 'complex'
;

ParameterTest: ID | STRING;
Variable: ID;

ConstructorStmt: lhs=ID '=' constructor=Constructor '(' ')' ;
Constructor: DataType ;

IdentifierOrNumber: Number | ID;

ArgList: '(' args+=Args[','] ')';
Args: ArgList | IdentifierOrNumber;

// TODO: remove
PythonStdLibStmt: PythonPrintStmt;

PythonPrintStmt: name='print' '('   (args+=PrintStmt[',']) ')';
PrintStmt: STRING | Expression;

// TODO: remove
ParameterValue: (value=IdentifierOrNumber |value=ArgList);
ParameterSingle: label=ParameterTest '=' value=ParameterValue;

AssignStmt: lhs=ID (trailer=Trailer)? '=' rhs=Expression;
MultiAssignStmt: lhs+=ID[','] '=' rhs=Expression;

StencilStmt: lhs=ID '=' 'stencil' '(' parameters+=ParameterSingle[','] ')' ;
ExecStmt: 
  lhs+=ID[','] '=' 'exec' '(' module=STRING ',' function=STRING ',' args=ArgList ')'
;

EvalStmt: lhs=ID '=' 'eval' '(' expression=STRING ')';

// TODO: improve
ThreadStmt: lhs=ID '=' func=ThreadFunction '(' ')';
ThreadFunction: 'thread_id' | 'thread_number';

RangeStmt:
  'range' '(' start=Expression ',' end=Expression (',' step=Expression)? ')'
;

IterableRange: RangeStmt | ID;

ForStmt: 'for' iterable+=ID[','] 'in' range=IterableRange ':' 
               body=SuiteStmt
;

WhileStmt:
  WHILE test=OrTest ':'
  body=SuiteStmt
;

IfStmt: 
  IF test=Test ':' body_true=SuiteStmt
  (body_elif*=ElifStmt)? 
  (ELSE ':' body_false=SuiteStmt)? 
;

ElifStmt:
  (ELIF test=Test ':' body=SuiteStmt) 
;

Test: op=OrTest (IF OrTest ELSE Test)?;
OrTest: op=AndTest (OR op=AndTest)*;
AndTest: op=Comparison (AND op=Comparison)*;
Comparison: op=Expression (op=OpComp op=Expression)*;

Expression: ArithmeticExpression;
ArithmeticExpression: op=Term (op=OpPlusOrMinus op=Term)*;
Term: op=Factor (op=OpFactor op=Factor)*;
Factor: Power | FactorSigned;
FactorSigned: (sign=OpFactorSigned)?  op=AtomExpr;
Power: op=AtomExpr '**' exponent=Factor;
AtomExpr: op=Atom (trailers*=Trailer)?;

Number: Float | INT;
Atom: 
   '(' op=Expression ')'
  | (op=Number) 
  | (op=ID) 
  | (op=STRING) 
  | (op=NONE)
  | (op=TRUE)
  | (op=FALSE)
  | '(' op=Test ')'
;

OpPlusOrMinus: '+' | '-';
OpFactor: '*' | '@' | '/' | '%' ;
OpFactorSigned: '+' | '-';
// <> isn't actually a valid comparison operator in Python. It's here for the
// sake of a __future__ import described in PEP 401 (which really works :-)
OpComp: 
    '>='
  | '<='
  | '<>'
  | '<'
  | '>'
  | '=='
  | '!='
  | 'in'
  | 'not' 'in'
  | 'is'
  | 'is' 'not'
;

Trailer: 
    (args=TrailerArgList)?
  | (args=TrailerSubscriptList)?
  | (args=TrailerDots)?
;

TrailerArgList: '(' args*=TrailerArg[','] ')';
TrailerSubscriptList: '[' args*=TrailerSubscript[','] ']';
TrailerDots: '.' args=ID;

TrailerArg: TrailerArgList | Expression;
TrailerSubscript: 
    TrailerSlice 
  | TrailerSliceLeft 
  | TrailerSliceRight 
  | TrailerSliceEmpty 
  | Expression
;

TrailerSliceLeft: (start=Expression ':');
TrailerSliceRight: (':' end=Expression);
TrailerSlice: (start=Expression ':' end=Expression);
TrailerSliceEmpty: (dots=':');

Float:
  /[-+]?[0-9]*\.[0-9]+([eE][-+]?[0-9]+)?\b/
;

BOOLEAN: TRUE | FALSE;

// indent/dedent are here since textX does not handle indentation yet
INDENT: 'indent';
DEDENT: 'dedent';

AND : 'and';
AS : 'as';
ASSERT : 'assert';
ASYNC : 'async';
AWAIT : 'await';
BREAK : 'break';
CLASS : 'class';
CONTINUE : 'continue';
DEL : 'del';
DEF : 'def';
ELIF : 'elif';
ELSE : 'else';
EXCEPT : 'except';
FALSE : 'False';
FINALLY : 'finally';
FOR : 'for';
FROM : 'from';
GLOBAL : 'global';
IF : 'if';
IN : 'in';
IS : 'is';
IMPORT : 'import';
LAMBDA : 'lambda';
NONE : 'None';
NONLOCAL : 'nonlocal';
NOT : 'not';
OR : 'or';
PASS : 'pass';
RETURN : 'return';
RAISE : 'raise';
TRUE : 'True';
TRY : 'try';
WHILE : 'while';
WITH : 'with';
YIELD : 'yield';
