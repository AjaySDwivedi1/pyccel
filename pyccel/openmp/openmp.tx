OpenMPModel:
  statements*=OpenmpStmt
;

OpenmpStmt: 
  '#@' 'omp' stmt=ParallelStmt 
;

// TODO: add if parallel
ParallelStmt:
  'parallel' clauses*=ParallelClause[',']
;

ParallelClause:
  ParallelNumThreadClause
  |
  ParallelDefaultClause
  |
  PrivateListClause
  |
  SharedListClause
  |
  FirstPrivateListClause
  |
  CopyinListClause
  |
  ReductionClause
  |
  ParallelProcBindClause
;

ParallelNumThreadClause:
 clause='num_threads' '(' thread=ThreadIndex ')'
;

ParallelDefaultClause:
 clause='default' '(' status=Status ')'
;

ParallelProcBindClause:
 clause='proc_bind' '(' status=ProcBind ')'
;

PrivateListClause:
 clause='private' '(' args+=ID[','] ')'
;

SharedListClause:
 clause='shared' '(' args+=ID[','] ')'
;

FirstPrivateListClause:
 clause='firstprivate' '(' args+=ID[','] ')'
;

CopyinListClause:
 clause='copyin' '(' args+=ID[','] ')'
;

ReductionClause:
 clause='reduction' '('op=ReductionOperator args+=ID[','] ')'
;

ProcBind:
  ('master' | 'close' | 'spread')
;

ReductionOperator:
  ('+' | '-' | '*' | '/')
;

Status:
  ('private' | 'firstprivate' | 'shared' | 'none')
;

ThreadIndex:
  (ID | INT)
;

NotaStmt:
  /.*$/
;
