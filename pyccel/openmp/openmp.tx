OpenMPModel:
  statements*=OpenmpStmt
;

OpenmpStmt: 
  '#@' 'omp' stmt=OpenmpConstruct
;

OpenmpConstruct:
  ParallelStmt 
  | 
  LoopStmt
;

// TODO: add if parallel
ParallelStmt:
  'parallel' clauses*=ParallelClause[',']
;

LoopStmt:
  'do' clauses*=LoopClause[',']
;

ParallelClause:
  ParallelNumThreadClause
  |
  ParallelDefaultClause
  |
  PrivateListClause
  |
  SharedListClause
  |
  FirstPrivateListClause
  |
  CopyinListClause
  |
  ReductionClause
  |
  ParallelProcBindClause
;

// TODO: LinearListClause
LoopClause:
  PrivateListClause
  |
  FirstPrivateListClause
  |
  LastPrivateListClause
//  |
//  LinearListClause
  |
  ReductionClause
  |
  ScheduleClause
  |
  CollapseClause
  |
  OrderedClause
;

ParallelNumThreadClause:
 clause='num_threads' '(' thread=ThreadIndex ')'
;

ParallelDefaultClause:
 clause='default' '(' status=Status ')'
;

ParallelProcBindClause:
 clause='proc_bind' '(' status=ProcBind ')'
;

PrivateListClause:
 clause='private' '(' args+=ID[','] ')'
;

SharedListClause:
 clause='shared' '(' args+=ID[','] ')'
;

FirstPrivateListClause:
 clause='firstprivate' '(' args+=ID[','] ')'
;

LastPrivateListClause:
 clause='lastprivate' '(' args+=ID[','] ')'
;

CopyinListClause:
 clause='copyin' '(' args+=ID[','] ')'
;

ReductionClause:
 clause='reduction' '('op=ReductionOperator ':' args+=ID[','] ')'
;

CollapseClause:
  clause='collapse' '(' n=INT ')'
;

OrderedClause:
  clause='ordered' ('(' n=INT ')')?
;

ScheduleClause:
  clause='schedule' '(' kind=Schedule (chunk_size=INT)? ')'
;

Schedule:
  ('static' | 'dynamic' | 'guided' | 'auto' | 'runtime' )
;

ProcBind:
  ('master' | 'close' | 'spread')
;

ReductionOperator:
  ('+' | '-' | '*' | '/')
;

Status:
  ('private' | 'firstprivate' | 'shared' | 'none')
;

ThreadIndex:
  (ID | INT)
;

NotaStmt:
  /.*$/
;
