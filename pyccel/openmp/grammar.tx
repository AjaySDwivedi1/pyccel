Openmp:
  statements*=OpenmpStmt
;

OpenmpStmt: 
  '#$' 'omp' stmt=OpenmpConstruct
;

OpenmpConstruct:
  ParallelStmt 
  | 
  LoopStmt
  | 
  SingleStmt
  |
  EndConstructClause
;

// TODO: add if parallel
ParallelStmt:
  'parallel' clauses*=ParallelClause
;

LoopStmt:
  'do' clauses*=LoopClause
;

SingleStmt:
  'single' clauses*=SingleClause
;

ParallelClause:
  ParallelNumThreadClause
  |
  ParallelDefaultClause
  |
  PrivateClause
  |
  SharedClause
  |
  FirstPrivateClause
  |
  CopyinClause
  |
  ReductionClause
  |
  ParallelProcBindClause
;

LoopClause:
  PrivateClause
  |
  FirstPrivateClause
  |
  LastPrivateClause
  |
  LinearClause
  |
  ReductionClause
  |
  ScheduleClause
  |
  CollapseClause
  |
  OrderedClause
;

SingleClause:
  PrivateClause
  |
  FirstPrivateClause
;

ParallelNumThreadClause:
 'num_threads' '(' thread=ThreadIndex ')'
;

ParallelDefaultClause:
 'default' '(' status=Status ')'
;

ParallelProcBindClause:
 'proc_bind' '(' status=ProcBind ')'
;

PrivateClause:
 'private' '(' args+=ID[','] ')'
;

SharedClause:
 'shared' '(' args+=ID[','] ')'
;

FirstPrivateClause:
 'firstprivate' '(' args+=ID[','] ')'
;

LastPrivateClause:
 'lastprivate' '(' args+=ID[','] ')'
;

CopyinClause:
 'copyin' '(' args+=ID[','] ')'
;

ReductionClause:
 'reduction' '('op=ReductionOperator ':' args+=ID[','] ')'
;

CollapseClause:
  'collapse' '(' n=INT ')'
;

// TODO: improve using lists. see specs
LinearClause:
  'linear' '(' val=ID ':' step=INT ')'
;

OrderedClause:
  'ordered' ('(' n=INT ')')?
;

ScheduleClause:
  'schedule' '(' kind=Schedule (',' chunk_size=INT)? ')'
;

EndConstructClause:
  'end' construct=OpenmpConstructs (simd='simd')? (nowait='nowait')?
;

Schedule:
  ('static' | 'dynamic' | 'guided' | 'auto' | 'runtime' )
;

ProcBind:
  ('master' | 'close' | 'spread')
;

ReductionOperator:
  ('+' | '-' | '*' | '/')
;

Status:
  ('private' | 'firstprivate' | 'shared' | 'none')
;

OpenmpConstructs:
  ('single' | 'parallel' | 'do')
;

ThreadIndex:
  (ID | INT)
;

NotaStmt:
  /.*$/
;
