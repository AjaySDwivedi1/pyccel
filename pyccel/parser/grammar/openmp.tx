Omp:
  statements*=OmpStatement
;

OmpStatement:
  '#$' 'omp' statement=OmpConstructOrDirective
;

////////////////////////////////////////////////////
//         Constructs and Directives
////////////////////////////////////////////////////
OmpConstructOrDirective:
      OmpParallelConstruct
    | OmpEndConstruct
;
////////////////////////////////////////////////////

////////////////////////////////////////////////////
//     Constructs and Directives definitions
////////////////////////////////////////////////////
OmpParallelConstruct:   name='parallel'  clauses*=AllOmpClauses;
OmpEndConstruct: 'end' name=OmpEndedConstruct;

////////////////////////////////////////////////////
//      Clauses for Constructs and Directives
////////////////////////////////////////////////////

AllOmpClauses:
      OmpIfClause
    | OmpNumThreadsClause
    | OmpDefaultClause
    | OmpPrivateClause
    | OmpFirstPrivateClause
    | OmpSharedClause
    | OmpCopyinClause
    | OmpReductionClause
    | OmpProcBindClause
    // TODO: | OmpAllocateClause
;
////////////////////////////////////////////////////



////////////////////////////////////////////////////
//              Clauses definitions
////////////////////////////////////////////////////
OmpIfClause: name='if' '(' (construct=ID)? arg=ParenthesizedExpression ')';
OmpNumThreadsClause: name='num_threads' '(' arg=ParenthesizedExpression ')';
OmpDefaultClause: name='default' '(' arg=DataSharingAttribute ')';
OmpPrivateClause: name='private' '(' args+=ID[','] ')';
OmpFirstPrivateClause: name='firstprivate' '(' args+=ID[','] ')';
OmpSharedClause: name='shared' '(' args+=ID[','] ')';
OmpCopyinClause: name='copyin' '(' args+=ID[','] ')';
OmpReductionClause: name='reduction' '(' 
                        (operator=ReductionOperator ':' args+=ID[','] version='4.5') // support for version 4.5 syntax
                      | ((modifier=ReductionModifier ',')? operator=ReductionOperator ':' args+=ID[','] version='5.0') // support for version 5.0 syntax
                    ')';
OmpProcBindClause: name='proc_bind' '(' arg=AffinityPolicy ')';

////////////////////////////////////////////////////
//              Clauses values
////////////////////////////////////////////////////
OmpEndedConstruct: 'parallel';
ParenthesizedExpression: (/.*(?=\))/); // match until the last closing parenthesis
DataSharingAttribute: ('shared' | 'none' | 'private' | 'firstprivate');
ReductionOperator: ('+' | '-' | '*' | '&' | '|' | '^' | '&&' | '||' | 'min' | 'max'); // TODO: those are only the implicitly declared ones for C and C++. Add the others for Fortran and support user-defined ones (see specs https://www.openmp.org/spec-html/5.0/openmpsu107.html#x140-581002)
AffinityPolicy: ('master' | 'close' | 'spread' | 'primary');
ReductionModifier: ('inscan' | 'task' | 'default');

////////////////////////////////////////////////////
