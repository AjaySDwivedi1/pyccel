Header:
  statements*=HeaderStmt
;

HeaderStmt: '#$' 'header' stmt=Statement;

Statement:
    FunctionMacroStmt 
  | VariableHeaderStmt
  | FunctionHeaderStmt 
  | ClassHeaderStmt
  | MetavarHeaderStmt
  | InterfaceStmt
;

TrailerSubscriptList: '[' args*=':' [','] ']';

DataType: ID|STAR;

Type: dtype=DataType (trailer=TrailerSubscriptList)?;
ListType : '[' dtype+=DataType[','] ']';
TypeHeader: Type|ListType ;
UnionTypeStmt:  dtype+=TypeHeader['|'] ;


VariableHeaderStmt: 'variable'  name=ID ('::')? dec=TypeHeader;

FunctionHeaderStmt: 
  (kind=FunctionKind)? (static=Static)? name=ID '(' decs*=UnionTypeStmt[','] ')' (results=HeaderResults)?
;

FunctionKind: 'function' | 'procedure' | 'method';
Static: 'static';
HeaderResults: 'results' '(' decs+=TypeHeader[','] ')'; 

ClassHeaderStmt: 'class'  name=ID '(' options+=ClassOptions[','] ')';

ClassOptions: 
    'abstract' 
  | 'private' 
  | 'public' 
  | 'iterable' 
  | 'with' 
  | 'hide' 
  | 'openmp'
  | 'openacc'
;

MetavarHeaderStmt: 'metavar'  name=ID '=' value=MetavarValues;
MetavarValues: BOOL|STRING;

InterfaceStmt: 'interface' name=ID '=' args+=ID['|'];

// **** macros ****
ListArgsStmt: args*=MacroArg[','];
ListResultsStmt: args*=ID[','];
ListAnnotatedArgsStmt: args*=MacroMasterArg[','];

MacroArg: arg=ID (optional='?')?;
MacroMasterArgBasic: MacroStmt | INT | NUMBER | ID;
MacroMasterArg: arg=MacroMasterArgBasic ('|' default=MacroMasterArgBasic)?;
MacroStmt: arg=ID '.' macro=AvailableMacro ('[' parameter=MacroParameter ']')?;
MacroParameter: INT | ID;

// TODO shall we add also results to the master call?
FunctionMacroStmt: 
  'macro' ('(' results=ListResultsStmt ')' ',')?
  name+=ID['.'] ('(' args=ListArgsStmt ')')? 
  ':='
  master_name+=ID['.'] ('(' master_args=ListAnnotatedArgsStmt ')')?
;

//VariableMacroStmt:
//  'macro' name+=ID['.'] ':=' master_name+=ID['.']
//;


// ****************

STAR: '*';
OPTIONAL: '!';
AvailableMacro: 
    'shape'
  | 'dtype'
;

